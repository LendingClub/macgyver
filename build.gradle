

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
/*allprojects {
    group 'io.macgyver'
    version = '1.0.0-SNAPSHOT'
}
*/
buildscript {
	ext {
		springBootVersion = '1.0.2.RELEASE'
	}
    repositories {
    	mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'spring-boot'
	
    
    group 'io.macgyver'
    version = "${MACGYVER_VERSION}"
    
    mainClassName = 'io.macgyver.core.ServerMain'
    
    
    
    configurations {
		all*.exclude group: 'commons-logging', module: 'commons-logging'
    	all*.exclude group: 'org.codehaus.groovy', module: 'groovy'
    	all*.exclude group: 'log4j'
    	all*.exclude group: 'xml-apis'
       
		provided
    }
    

 
	sourceSets {
	    main.compileClasspath += configurations.provided
	    test.compileClasspath += configurations.provided
	    test.runtimeClasspath += configurations.provided
	}
 
	
    repositories {
  
        mavenCentral()
        mavenLocal()
		jcenter()

    }
    
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
	
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
	
	artifacts {
	        archives sourcesJar, javadocJar
	}
    eclipse {
        jdt {
            //if you want to alter the java versions (by default they are configured with gradle java plugin settings):
            sourceCompatibility = 1.7
            targetCompatibility = 1.7
        }
    }
	uploadArchives {
	    repositories {
	        mavenDeployer {
	            name = 'macgyver-bintray'
                if (!project.hasProperty('bintrayUser')) {
                    project.ext['bintrayUser']=""
                }
                if (!project.hasProperty('bintrayKey')) {
                    project.ext['bintrayKey']=""
                }
	            repository(id: 'macgyver-bintray', url: 'https://api.bintray.com/maven/robschoening/io-macgyver/io-macgyver'){
	                authentication(userName: bintrayUser, password: bintrayKey)
				}
                pom.project {
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                
                }
	        }
	    }
	}

    
    
}

